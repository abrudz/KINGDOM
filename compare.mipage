:Class compare : MiPage

    ∇ Compose;Text;ibm;dyalog;miserver;lines;table;Dyalog
      :Access Public
      Add _.title'6 KINGDOM: compare'
      Add _.style ScriptFollows
      ⍝ @font-face {font-family: "APL385 Unicode";
      ⍝             src: local("APL385 Unicode"),url(/Apl385.woff)format("woff");}
      ⍝ *          {margin: 0; padding: 0;}
      ⍝ table      {table-layout: fixed; width: 100%; border-collapse: collapse;}
      ⍝ th         {position: -webkit-sticky; position: sticky; top: 0;
      ⍝             text-shadow: 0px 0px 1px black,0px 0px 1px black,0px 0px 1px black,0px 0px 1px black;
      ⍝             font-family: Verdana, Geneva, sans-serif; color: white; font-size: xx-large;}
      ⍝ td         {font-family: "APL385 Unicode", monospace; font-size: small;
      ⍝             white-space: pre; overflow: hidden; text-overflow: ellipsis;}
      ⍝ b          {font-weight: normal; color: #08F;}
      ⍝ i          {font-style: normal; color: #F80;}
      Text←{(⎕UCS 13)(1↓¨⊢⊂⍨=)¯1⌽3↓¯4↓'whitespace' 'preserve'⎕XML 1 3⍴0 'x'(⊃⎕NGET #.Boot.AppRoot,⍵)}
      (ibm dyalog miserver)←Text¨'KINGDOM.txt' 'KINGDOM.dyalog' 'index.mipage'
      Dyalog←'⎕AI\[3\]' '⎕RL←X,1' '(,?\(⍕)([A-Z+]+)\),'⎕R'<i>&</i>' '<i>&</i>' '<i>\1</i>\2<i>),</i>'
      dyalog←Dyalog dyalog
      lines←(≢dyalog)-2+≢miserver
      (⊃dyalog)←' ' ⋄ (⊃⌽dyalog)←' '
      miserver←Dyalog miserver
      (lines↑1⌽miserver)←'<b>'∘,¨(lines↑1⌽miserver),¨⊂'</b>'
      (lines↓miserver)←'(0 )?∆' '⍙' '(Quote)?Quad'⎕R'<b>&</b>'⊢lines↓miserver
      table←Add _.Table('IBM APL\360' 'DYALOG APL' 'MISERVER 3.0'⍪⍉↑ibm dyalog miserver)
      table.HeaderRows←1
    ∇

:EndClass
